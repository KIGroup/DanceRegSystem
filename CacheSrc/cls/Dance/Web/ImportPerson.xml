<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Dance.Web.ImportPerson">
<Super>%CSP.Page</Super>
<TimeCreated>63331,43978.703959</TimeCreated>

<Method name="OnPage">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	&html<<html><head></head><body style='font-family: Helvetica,Arial,sans-serif; font-size: 14px; line-height: 20px; color: #333; margin: 0;'>>
	
	set result = ##class(%ZEN.proxyObject).%New()
	set result.success = 0
	set result.errorMessages = ##class(%ListOfObjects).%New()
	set line = ""
	try{
		set fileName = %request.MimeData("FileStream", 1).FileName
		set stream = %request.MimeData("FileStream", 1)
	
		set gender = "Male"
		
		if ($FIND(fileName, "female") '= 0) || ($FIND(fileName, "girl") '= 0) || ($FIND(fileName, "woman") '= 0){
			set gender = "Female"
		}
		
		set newStream = ##class(%CSP.CharacterStream).%New()
		
		while 'stream.AtEnd {
			set line = $REPLACE(stream.ReadLine(), $C(10), $C(13)_$C(10))
			set line = $ZCVT(line, "I", "CP1251")
			do newStream.Write(line)
		}
		kill stream
		
		while 'newStream.AtEnd {
			set line = newStream.ReadLine()
			
			if ($FIND(line,";") = 0) continue
			
			set st = ##class(Dance.Data.PersonUDSR).UpdateFromString(line, gender)
			if $$$ISOK(st){
				set result.success = result.success + 1
			}
			else{
				set error = ##class(%ZEN.proxyObject).%New()
				set error.status = $system.Status.GetErrorText(st)
				set error.line = line
				do result.errorMessages.Insert(error)
			}
		}
		
		w "<p style='margin: 0 0 10px 0;'><strong>Результат импорта данных</strong></p>"
		w "<p style='margin: 0 0 10px 0;'>Всего записей в файле: "_(result.success + result.errorMessages.Count())_"</p>"
		w "<p style='margin: 0 0 10px 0;'>Ипортировано: "_(result.success)_"</p>"
		w "<p style='margin: 0 0 10px 0;'>Не импортировано: "_(result.errorMessages.Count())_"</p>"
		
		if (result.errorMessages.Count() '= 0){
			w "<br><br>Список ошибочных записей: "
			for i=1:1:result.errorMessages.Count(){
				w "<br>"_i_". "_result.errorMessages.GetAt(i).line
				w "<br>"_result.errorMessages.GetAt(i).status
				w "<br>"
			}
		}
		
		set ^Settings("Dance","UDSRPersonsLastUpdated") = $ZDT($ZTS, 3)
	}catch(ex){
		set st = ex.AsStatus()	
	}
	
	if $$$ISERR(st){
		w line_"<br>"
		w $SYSTEM.Status.GetErrorText(st)
	}
	
	&html<</body></html>>
	Quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>
